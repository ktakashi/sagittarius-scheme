; -*- mode:iss -*-
; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Sagittarius"
#define MyAppVersion "@SAGITTARIUS_VERSION@"
#define MyAppPublisher "Takashi Kato"
#define MyAppURL "https://bitbucket.org/ktakashi/sagittarius-scheme/"
#define MyAppExeName "sash.exe"
#define SchemeFileLoaderFile "scheme-file-loader.scm"
#define SchemeFileLoader "Scheme File Loader"
#define BoehmGCVersion "@GC_VERSION@"
#define BuiltDir "@CMAKE_RUNTIME_OUTPUT_DIRECTORY@"
#define SrcDir   "@PROJECT_SOURCE_DIR@"
#define BinDir   "@PROJECT_BINARY_DIR@"
#define TargetArch "@SAGITTARIUS_ARCH@"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={@SAGITTARIUS_APP_ID@
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
UsePreviousAppDir=no
DisableDirPage=auto
AlwaysShowDirOnReadyPage=yes
PrivilegesRequiredOverridesAllowed=commandline dialog
LicenseFile={#SrcDir}\COPYING
OutputBaseFilename=setup_sagittarius_{#MyAppVersion}{#TargetArch}
Compression=lzma
SolidCompression=yes
ChangesAssociations=yes
WizardStyle=modern
@SAGITTARIUS_INSTALLER_MODE@
@SAGITTARIUS_INSTALLER_ARCH@

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "jp"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "nl"; MessagesFile: "compiler:Languages\Dutch.isl"

[CustomMessages]
AdditionalRegistry=Registry association:
jp.AdditionalRegistry=レジストリに登録する:
nl.AdditionalRegistry=Vereniging van het register:

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "desktopiconLoader"; Description: "{cm:CreateDesktopIcon} ({#SchemeFileLoader})"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 6.1
Name: "registry"; Description: "{cm:AssocFileExtension, {#MyAppName}, .scm}"; GroupDescription: "{cm:AdditionalRegistry}"; Flags: unchecked

[Files]
Source: "{#BuiltDir}\sash.exe"; DestDir: "{app}"; DestName: "{#MyAppExeName}"; Flags: ignoreversion
Source: "{#BuiltDir}\sagittarius.dll"; DestDir: "{app}"; DestName: "sagittarius.dll"; Flags: ignoreversion
Source: "{#BuiltDir}\src\sagittarius.lib"; DestDir: "{app}"; DestName: "sagittarius.lib"; Flags: ignoreversion
Source: "{#BuiltDir}\*.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SrcDir}\src\*.h"; DestDir: "{app}\include"; Excludes: "os*"; Flags: ignoreversion recursesubdirs createallsubdirs
; Well Boehm GC header also need to be installed.
;; probably we don't need these 2 lines anymore
Source: "{#BinDir}\gc-{#BoehmGCVersion}\include\*.h"; DestDir: "{app}\include"; Excludes: "private,extra,cord.h,ec.h,javaxfc.h"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#BuiltDir}\gc.lib"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SrcDir}\win\{#SchemeFileLoaderFile}"; DestDir: "{app}"; Flags: ignoreversion


Source: "{#SrcDir}\lib\*"; DestDir: "{app}\lib"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\sitelib\*"; DestDir: "{app}\sitelib"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#BuiltDir}\modules\*.dll"; DestDir: "{app}\modules"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#BinDir}\doc\sagittarius-ref.html"; DestDir: "{app}\doc"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\doc\lib\*"; DestDir: "{app}\doc\lib"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; TODO: I don't want to write install script every time when I added ext/* module
Source: "{#SrcDir}\ext\socket\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\ext\threads\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\ext\time\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\ext\process\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\ext\ffi\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm,libffi*"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\ext\crypto\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm,libtom*,tests"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\ext\odbc\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\ext\zlib\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm,zlib-*,data"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\ext\termios\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SrcDir}\ext\filewatch\*.scm"; DestDir: "{app}\lib"; Excludes: "test*.scm"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{#SchemeFileLoader}"; Filename: "{app}\{#SchemeFileLoaderFile}"
Name: "{group}\reference.html"; Filename: "{app}\doc\sagittarius-ref.html"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{commondesktop}\{#SchemeFileLoader}"; Filename: "{app}\{#SchemeFileLoaderFile}"; Tasks: desktopiconLoader
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon

[Registry]
Root: HKCR; Subkey: ".scm"; ValueType: string; ValueName: ""; ValueData: "{#MyAppName}"; Flags: uninsdeletevalue; Tasks: registry
Root: HKCR; Subkey: "{#MyAppName}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppName} {#MyAppVersion}"; Flags: uninsdeletekey; Tasks: registry
Root: HKCR; Subkey: "{#MyAppName}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"; Tasks: registry
Root: HKCR; Subkey: "{#MyAppName}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"" %*"; Tasks: registry

;[Run]
;Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, "&", "&&")}}"; Flags: nowait postinstall skipifsilent

