# 
# Utilities.cmake
# 
#   Copyright (c) 2025  Takashi Kato <ktakashi@ymail.com>
# 
#   Redistribution and use in source and binary forms, with or without
#   modification, are permitted provided that the following conditions
#   are met:
# 
#   1. Redistributions of source code must retain the above copyright
#      notice, this list of conditions and the following disclaimer.
# 
#   2. Redistributions in binary form must reproduce the above copyright
#      notice, this list of conditions and the following disclaimer in the
#      documentation and/or other materials provided with the distribution.
# 
#   THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
#   "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
#   LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
#   A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
#   OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
#   SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
#   TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
#   PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
#   LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
#   NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
#   SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
# 

# In file replacement
MACRO(IN_FILE_REPLACE PATH OLD NEW)
  MESSAGE(STATUS "Replacing ${OLD} to ${NEW} in ${PATH}")
  FILE(READ ${PATH} _FILE_CONTENTS)
  SET(_BACKUP_FILE "${PATH}.orig")
  IF (NOT EXISTS ${_BACKUP_FILE})
    FILE(WRITE "${_BACKUP_FILE}" "${_FILE_CONTENTS}")
  ENDIF()
  STRING(REPLACE "${OLD}" "${NEW}" _FILE_CONTENTS "${_FILE_CONTENTS}")
  FILE(WRITE "${PATH}" "${_FILE_CONTENTS}")
ENDMACRO(IN_FILE_REPLACE)

MACRO(DOWNLOAD)
  CMAKE_PARSE_ARGUMENTS(DL_ARGS "" "RETRY_COUNT" "" ${ARGN})
  LIST(LENGTH DL_ARGS_UNPARSED_ARGUMENTS _UNPARSED_COUNT)
  IF (NOT _UNPARSED_COUNT EQUAL 2)
    MESSAGE(FATAL_ERROR "URL and destination are missing")
  ENDIF()
  LIST(GET DL_ARGS_UNPARSED_ARGUMENTS 0 DL_URL)
  LIST(GET DL_ARGS_UNPARSED_ARGUMENTS 1 DL_ARGS_OUTFILE)

  IF (NOT DL_ARGS_OUTFILE)
    MESSAGE(FATAL_ERROR "OUTFILE is missing")
  ENDIF()

  IF (DL_ARGS_RETRY_COUNT)
    SET(RETRY_COUNT ${DL_ARGS_RETRY_COUNT})
  ELSE()
    SET(RETRY_COUNT 3)
  ENDIF()

  MATH(EXPR ATTEMPTS "${RETRY_COUNT} + 1")
  SET(CURRENT_ATTEMPTS 0)
  SET(DOWNLOAD_SUCCESSFUL FALSE)
  MESSAGE(STATUS "Downloading ${DL_URL}")
  WHILE (CURRENT_ATTEMPTS LESS ATTEMPTS)
    MATH(EXPR CURRENT_ATTEMPTS "${CURRENT_ATTEMPTS} + 1")
    FILE(DOWNLOAD "${DL_URL}" "${DL_ARGS_OUTFILE}"
      STATUS DOWNLOAD_STATUS LOG DOWNLOAD_LOG)
    LIST(GET DOWNLOAD_STATUS 0 STATUS_CODE)
    LIST(GET DOWNLOAD_STATUS 1 STATUS_MESSAGE)
    IF (STATUS_CODE EQUAL 0)
      SET(DOWNLOAD_SUCCESSFUL TRUE)
      BREAK()
    ELSE()
      IF (CURRENT_ATTEMPTS LESS ATTEMPTS)
	MESSAGE(WARNING "Retry download (Attempt ${CURRENT_ATTEMPTS}/${ATTEMPTS})")
      ELSE()
	MESSAGE(FATAL_ERROR "Failed to download ${DL_URL} after ${ATTEMPTS}")
      ENDIF()
    ENDIF()
  ENDWHILE()
ENDMACRO(DOWNLOAD)
